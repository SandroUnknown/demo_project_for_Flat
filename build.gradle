plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.12.0'
}

group = 'gmail.sandrounknown1991'
version = '1.0-SNAPSHOT'

def junitVersion = "5.11.1",
    selenideVersion = "7.5.1",
    fakerVersion = "1.0.2",
    allureVersion = "2.29.0"


repositories {
    mavenCentral()
}

dependencies {
    testImplementation(
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "com.codeborne:selenide:$selenideVersion",
            "com.github.javafaker:javafaker:$fakerVersion",
            "io.qameta.allure:allure-selenide:$allureVersion"
    )
}

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task end_to_end_test(type: Test) {
    useJUnitPlatform {
        includeTags("End-to-end")
    }
}

task smoke_test(type: Test) {
    useJUnitPlatform {
        includeTags("Smoke")
    }
}

task negative_test(type: Test) {
    useJUnitPlatform {
        includeTags("Negative")
    }
}

task smoke_and_negative_test(type: Test) {
    useJUnitPlatform {
        includeTags("Smoke", "Negative")
    }
}